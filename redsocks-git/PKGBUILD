# Maintainer: Anton S <fluffylime at gmail dot com>
# Contributor: crequill <crequill at worldonline dot fr>
# Contributor: Tom Vincent <http://tlvince.com/contact/>

pkgname=redsocks-git
pkgver=20120805
pkgrel=2
pkgdesc='Transparent redirector of any TCP connection to proxy using your firewall'
arch=('i686' 'x86_64')
url='http://darkk.net.ru/redsocks/'
license=('GPL3')
depends=('libevent')
makedepends=('git')
conflicts=('redsocks')
provides=('redsocks')
install=redsocks.install
backup=('etc/redsocks.conf'
        'etc/conf.d/redsocks'
        'etc/iptables/redsocks.rules')
source=('redsocks.rc'
        'redsocks.conf'
        'redsocks.confd'
        'redsocks.tmpfiles'
        'redsocks.rules')
md5sums=('212704393c4dd515c7c39a611f1c4555'
         '51ae01381791cc6f3f9e8a0fd0f8822c'
         'ac22995cb50cafd5e2bc2f528fa7d026'
         '9be5e8bea768b1a8fdec55a8e8af33fd'
         '0b9526c43ea2a2373e333ef5f8cf91e8')

_gitroot='http://github.com/darkk/redsocks.git'
_gitname=redsocks

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."
  if [[ -d "${_gitname}" ]]; then
      (cd "${_gitname}" && git pull origin)
      msg "The local files are updated."
  else
      git clone "${_gitroot}" "${_gitname}"
  fi
  msg "GIT checkout done or server timeout"

  cd "${_gitname}"
  make
}

package() {
  install -Dm755 "${srcdir}/redsocks.rc" "${pkgdir}/etc/rc.d/redsocks"
  install -Dm644 "${srcdir}/redsocks.conf" "${pkgdir}/etc/redsocks.conf"
  install -Dm644 "${srcdir}/redsocks.confd" "${pkgdir}/etc/conf.d/redsocks"
  install -Dm644 "${srcdir}/redsocks.rules" "${pkgdir}/etc/iptables/redsocks.rules"
  install -Dm644 "${srcdir}/redsocks.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/redsocks.conf"

  install -Dm755 "${srcdir}/${_gitname}/redsocks" "${pkgdir}/usr/bin/redsocks"
  install -Dm644 "${srcdir}/${_gitname}/README" "${pkgdir}/usr/share/doc/redsocks/README"
  install -Dm644 "${srcdir}/${_gitname}/redsocks.conf.example" "${pkgdir}/usr/share/doc/redsocks/redsocks.conf.example"
}

