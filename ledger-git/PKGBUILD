# Based on PKGBUILD by: Nathan Jones <nathanj@insightbb.com>
# Contributor: Johann Kl√§hn <kljohann@gmail.com>

pkgname=ledger-git
pkgdesc="CLI double-entry accounting tool"
pkgver=20120521
pkgrel=1

# uncomment the following line if you want a debug build.
#_build=debug

# uncomment the following line if you want to build the 'next' branch.
#_branch=next

depends=('gmp' 'pcre' 'expat' 'mpfr' 'boost' 'libedit')
makedepends=('git' 'sed' 'python2' 'cmake')
url="http://ledger-cli.org"
license=('BSD')
arch=('i686' 'x86_64')
provides=('ledger')
conflicts=('ledger')
[[ $_build == "debug" ]] && options=('!strip')
install=ledger.install
source=()
md5sums=()

_gitroot="git://github.com/jwiegley/ledger.git"
_gitname="ledger"

build() {
  cd $srcdir

  msg "Connecting to GIT server..."
  if [[ -d $_gitname ]]; then
    (cd $_gitname && git pull origin)
  else
    git clone -b ${_branch:-master} $_gitroot $_gitname
  fi
  msg "GIT checkout done or server timeout"
  msg "Starting make..."

  rm -rf $_gitname-build
  git clone $_gitname $_gitname-build
  cd $_gitname-build

  find -iname '*.py' -execdir sed -i 's/^#!.*python/#!\/usr\/bin\/python2/' '{}' \;
  sed -i 's/^#!.*python/#!\/usr\/bin\/python2/' ./acprep

  ./acprep submodule

  # FIXME: '--no-python' does not work at the moment. (2012-05-20)
  ./acprep ${_build:-opt} --prefix=/usr make
}

package () {
  cd "$srcdir/$_gitname-build"
  make DESTDIR=$pkgdir install

  rm -rf $pkgdir/usr/share/locale

  # handle the dir file in the install script
  rm -rf $pkgdir/usr/share/info/dir

  install -D -m644 doc/LICENSE $pkgdir/usr/share/licenses/custom/ledger-git/license.txt
}
