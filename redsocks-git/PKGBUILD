# Maintainer: Anton S <fluffylime at gmail dot com>
# Contributor: crequill <crequill at worldonline dot fr>
pkgname=redsocks-git
pkgver=20110527
pkgrel=3
pkgdesc='Transparent redirector of any TCP connection to proxy using your firewall'
arch=('i686' 'x86_64')
url='http://darkk.net.ru/redsocks/'
license=('GPL3')
depends=('libevent')
makedepends=('git')
conflicts=('redsocks')
provides=('redsocks')
backup=("etc/redsocks.conf")
source=(redsocks.rc)
md5sums=('83d4485ebac0777c02ae0a6b9517691f')

# Original source doesn't support libevent2
# _gitroot='http://github.com/darkk/redsocks.git'
# Fork from bjin does
_gitroot='http://github.com/bjin/redsocks.git'
_gitbranch=libevent2-fix
_gitname=redsocks

build() {
  cd "${srcdir}"

  msg "Connecting to GIT server...."
  if [[ -d "${_gitname}" ]]; then
      (cd "${_gitname}" && git pull origin)
      msg "The local files are updated."
  else
      git clone "${_gitroot}" "${_gitname}" -b "${_gitbranch}"
  fi
  msg "GIT checkout done or server timeout"

  cd "${_gitname}"
  make
}

package() {
  install -m755 -D "${srcdir}/${_gitname}/redsocks" "${pkgdir}/usr/bin/redsocks"
  install -m755 -d "${pkgdir}/usr/share/doc/redsocks"
  install -m644 -t "${pkgdir}/usr/share/doc/redsocks" ${srcdir}/${_gitname}/{README,redsocks.conf.example}
  install -m755 -d "${pkgdir}/etc"
  install -m644 -T "${srcdir}/${_gitname}/redsocks.conf.example" "${pkgdir}/etc/redsocks.conf"
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 -T "${srcdir}/redsocks.rc" "${pkgdir}/etc/rc.d/redsocks"
}

