# Maintainer: Tom Vincent <http://tlvince.com/contact>
pkgname=gollum
pkgver=2.4.5
pkgrel=1
pkgdesc="A simple, Git-powered wiki."
arch=('any')
url="http://github.com/github/gollum"
license=('MIT')
depends=('ruby' 'ruby-grit>=2.5.0' 'ruby-github-markup>=0.7.4'
'ruby-github-markup<1.0.0' 'ruby-github-markdown>=0.5.3'
'ruby-pygments.rb>=0.3.2' 'ruby-sinatra>=1.3.3' 'ruby-mustache>=0.99.4'
'ruby-mustache<1.0.0' 'ruby-sanitize>=2.0.3' 'ruby-nokogiri>=1.5.5'
'ruby-useragent>=0.4.15' 'ruby-stringex>=1.5.1')
makedepends=('rubygems')
source=(http://rubygems.org/downloads/$pkgname-$pkgver.gem)
noextract=($pkgname-$pkgver.gem)
md5sums=('cb2d992484b83c9b7a2997dab76664ad')

package() {
  cd "$srcdir"
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" $pkgname-$pkgver.gem
}
